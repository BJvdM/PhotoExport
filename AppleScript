set theDestRootText to "Media:Pictures2:"
set theDestRoot to (theDestRootText) as alias
set idx to 0

set originalVolume to output volume of (get volume settings)
set volume output volume 0

tell application "iPhoto"
	
	select album "Mauritius 2014"
	
	set thePhotos to (photos in album "Mauritius 2014" whose not (name of its keywords contains "OK"))
	
	repeat with thisPhoto in thePhotos
		
		set thisPhotoDate to thisPhoto's date
		set thisPhotoYear to (year of thisPhotoDate)
		set thisPhotoMonth to (month of thisPhotoDate)
		
		set theDestFolderText to (theDestRoot as text) & (thisPhotoYear as text) & ":"
		set theKeys to name of (keywords of thisPhoto)
		
		if theKeys contains "Professional" then
			set theDestFolder2Text to theDestFolderText & (thisPhotoMonth as integer) & "_Professional" & ":"
		else if theKeys contains "Holiday" then
			set theDestFolder2Text to theDestFolderText & (thisPhotoMonth as integer) & "_Holiday" & ":"
			
		else
			set theDestFolder2Text to theDestFolderText & (thisPhotoMonth as integer) & ":"
		end if
		set thisPhotoFile to image path of thisPhoto
		
		tell application "Finder"
			
			if not (exists (folder (theDestFolderText))) then
				set theDestFolder to make new folder at theDestRoot with properties {name:(thisPhotoYear as text)}
			else
				set theDestFolder to theDestFolderText as alias
			end if
			
			if not (exists (folder (theDestFolder2Text))) then
				if theKeys contains "Professional" then
					set newFolder2 to make new folder at theDestFolder with properties {name:((thisPhotoMonth as integer) & "_Professional") as text}
				else if theKeys contains "Holiday" then
					set newFolder2 to make new folder at theDestFolder with properties {name:((thisPhotoMonth as integer) & "_Holiday") as text}
				else
					set newFolder2 to make new folder at theDestFolder with properties {name:(thisPhotoMonth as integer)}
				end if
			end if
			
			set thisFile to POSIX file thisPhotoFile as alias
			set newFile to (duplicate thisFile to folder (theDestFolder2Text) with replacing)
			
			set theDate to short date string of thisPhotoDate
			set text item delimiters of AppleScript to "/"
			set theDateItems to text items of theDate
			set text item delimiters of AppleScript to "-"
			set theDateString to theDateItems as string
			
			set theTime to time string of thisPhotoDate
			set text item delimiters of AppleScript to ":"
			set theTimeItems to text items of theTime
			if length of item 1 of theTimeItems = 1 then
				set item 1 of theTimeItems to "0" & item 1 of theTimeItems
			end if
			set text item delimiters of AppleScript to " "
			set theSeconds to text items of (item 3 of theTimeItems)
			set text item delimiters of AppleScript to ""
			if item 2 of theSeconds = "PM" then
				if (item 1 of theTimeItems as integer) < 12 then
					set item 1 of theTimeItems to ((item 1 of theTimeItems as integer) + 12) as text
				end if
			end if
			set theTimeString to (text items 1 thru 2 of theTimeItems as string) & (text items 1 thru 1 of theSeconds as string)
			
			set idx_Date to 1
			
			set newFileString to (folder of newFile as string) & theDateString & " " & theTimeString & "_" & (idx_Date as string) & "." & (name extension of newFile as string)
			
			repeat
				if exists ((POSIX path of newFileString) as POSIX file) then
					set idx_Date to idx_Date + 1
					set newFileString to (folder of newFile as string) & theDateString & " " & theTimeString & "_" & (idx_Date as string) & "." & (name extension of newFile as string)
				else
					exit repeat
				end if
			end repeat
			
			set name of newFile to theDateString & " " & theTimeString & "_" & (idx_Date as string) & "." & (name extension of newFile as string)
			
		end tell
		
		select thisPhoto
		assign keyword string "OK"
		
		set idx to idx + 1
		
	end repeat
	
end tell

tell application "Finder"
	set logFolder to (container of (path to me)) as text
end tell

set logFile to open for access logFolder & "results.log" with write permission
write ((current date) as text) & ", " & (idx as text) & " photos were copied" & return starting at (1 + (get eof logFile)) to logFile
close access logFile


set volume output volume originalVolume
